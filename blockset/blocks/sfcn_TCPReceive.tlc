%%***************************************************************************************
%% file         sfcn_TCPReceive.tlc
%% brief        Target Language Compiler file that contains the code generation specifics
%%              for an S-function with the same name.
%%
%%---------------------------------------------------------------------------------------
%%                          C O P Y R I G H T
%%---------------------------------------------------------------------------------------
%%  Copyright 2024 (c) by GOcontroll      http://www.gocontroll.com     All rights reserved
%%
%%---------------------------------------------------------------------------------------
%%                            L I C E N S E
%%---------------------------------------------------------------------------------------
%% Permission is hereby granted, free of charge, to any person obtaining a copy of this
%% software and associated documentation files (the "Software"), to deal in the Software
%% without restriction, including without limitation the rights to use, copy, modify, merge,
%% publish, distribute, sublicense, and/or sell copies of the Software, and to permit
%% persons to whom the Software is furnished to do so, subject to the following conditions:
%%
%% The above copyright notice and this permission notice shall be included in all copies or
%% substantial portions of the Software.
%%
%% THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
%% INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR
%% PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE
%% FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR
%% OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
%% DEALINGS IN THE SOFTWARE.
%% endinternal
%%
%%***************************************************************************************

%implements sfcn_TCPReceive "C"
%include "tcp_common.tlc"

%% Function: BlockTypeSetup ==========================================================
%%
%% Purpose:
%%      Code generation for configuration
%%
%function BlockTypeSetup(block, system) void
	%<TcpCommonBlockTypeSetup()>
%endfunction

%function Start(block, system) Output
	%assign socket_id = block.SFcnParamSettings.socket_id
	%assign socket = "::tcp_%<socket_id>"
	%if EXISTS(%<socket>) == 0
		%assign error = "No TCP Configuration block present with the socket ID %<socket_id>"
		%<LibBlockReportError(block, error)>
	%endif
	%assign receive_socket = "::tcp_receive_%<socket_id>"
	%if EXISTS(%<receive_socket>) == 0
		%assign %<receive_socket> = 1
	%else
		%assign error = "There is already a TCP Receive block for the socket ID %<socket_id>, there is only supposed to be one receiver per socket."
		%<LibBlockReportError(block,error)>
	%endif
%endfunction

%% Function: Output ==========================================================
%%
%% Purpose:
%%      Code generation for signal output
%%
%function Outputs(block, system) Output
	%assign bytes_read = LibBlockOutputSignal(2, "", "", 0) %% uint32
	%assign receive_buffer = LibBlockOutputSignalAddr(1, "", "", 0) %% uint8[]
	%assign receive_buffer_size = LibBlockOutputSignalWidth(1)
	%assign socket_mode_par = LibBlockParameterValue(socket_mode, 0)
	%assign socket_id = block.SFcnParamSettings.socket_id
	%if %<socket_mode_par> == 1
		%assign socket = "tcp_socket_%<socket_id>_remote"
	%else
		%assign socket = "tcp_socket_%<socket_id>"
	%endif
	%assign socket_state = "tcp_socket_%<socket_id>_state"
	{
		if (%<socket_state> == connected) {
			int res;
			while(true){
				res = recv(%<socket>, %<receive_buffer>, %<receive_buffer_size>, MSG_DONTWAIT | MSG_NOSIGNAL);
				if ((res < 0 && !(errno == EAGAIN || errno == EWOULDBLOCK)) || res == 0) { %%unexpected error or socket closed, close socket
					%<socket_state> = not_connected;
					fprintf(stderr, "receive error: %s\n", strerror(errno));
					close(%<socket>);
					break;
				} else if (res < 0) { %%socket empty
					break;
				}
				%<bytes_read> = res;
				%% Call a function to process the received message via function-call subsystem
				%foreach callIdx = NumSFcnSysOutputCalls
					%if LibIsEqual(SFcnSystemOutputCall[callIdx].BlockToCall,"unconnected")
						%continue
					%endif
					%% call the downstream system
					%<LibBlockExecuteFcnCall(block, callIdx)>\
				%endforeach
			}
		}
	}
%endfunction