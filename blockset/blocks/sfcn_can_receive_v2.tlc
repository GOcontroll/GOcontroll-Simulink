%% Copyright (C) 2014 Czech Technical University in Prague
%%
%% Authors:
%%     Michal Horn  <hornmich@fel.cvut.cz>
%%     Michal Sojka <sojkam1@fel.cvut.cz>
%%     Pavel Pisa <pisa@cmp.felk.cvut.cz> - author of SocketCAN port
%%
%% Redistribution and use in source and binary forms, with or without
%% modification, are permitted provided that the following conditions are
%% met:
%%
%% 1. Redistributions of source code must retain the above copyright
%%    notice, this list of conditions and the following disclaimer.
%%
%% 2. Redistributions in binary form must reproduce the above copyright
%%    notice, this list of conditions and the following disclaimer in the
%%    documentation and/or other materials provided with the
%%    distribution.
%%
%% 3. Neither the name of the copyright holder nor the names of its
%%    contributors may be used to endorse or promote products derived
%%    from this software without specific prior written permission.
%%
%% THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
%% "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
%% LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
%% A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
%% HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
%% SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
%% LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
%% DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
%% THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
%% (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
%% OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
%%
%% File : sfunction_canreceive.tlc
%% Abstract:
%%     TLC file for configuring the CAN RX buffer on the TI TMS570LS3137 MCU
%%
%% References:
%%     BlockTypeSetup() : refs/rtw_tlc.pdf p. 277
%%     Outputs()        : refs/rtw_tlc.pdf p. 281


%% Adopted for use by GOcontroll 2024		http://www.gocontroll.com		All right reserved
%% File : sfcn_can_receive.tlc
%% Brief : help functions for the can tlcs

%implements sfcn_can_receive_v2 "C"

%include "can_common.tlc"

%% Function: BlockTypeSetup ====================================================
%function BlockTypeSetup(block, system) void
    %<CommonBlockTypeSetup(block, system)>
	%<CANCommonBlockTypeSetup()>
%endfunction

%% Function: BlockInstanceSetup ================================================
%function BlockInstanceSetup(block, system) void

	%assign module_id_par = LibBlockParameterValue(module_id, 0)

	%assign can_recv_socket = "::can_%<module_id_par>_recv"
	%if EXISTS(%<can_recv_socket>) == 1
		%assign error = "2 or more CAN receive blocks are listening on CAN%<module_id_par>, this is not allowed"
		%<LibBlockReportError(block, error)>
	%else
		%assign %<can_recv_socket> = 1
	%endif

	%assign can_socket = "::can_%<module_id_par>"
	%if EXISTS(%<can_socket>) == 0
		%assign %<can_socket> = 1
		%openfile tmpBuf
			int can_%<module_id_par>;
		%closefile tmpBuf

		%<LibSetSourceFileSection(LibGetModelDotCFile(), "UserTop", tmpBuf)>
	%endif
%endfunction

%% Function: Start =============================================================
%function Start(block, system) Output
	%assign module_id_par = LibBlockParameterValue(module_id, 0)

	%assign can_socket = "::can_%<module_id_par>_init"
	%if EXISTS(%<can_socket>) == 0
		%assign %<can_socket> = 1
		{
			struct sockaddr_can addr;
			struct ifreq ifr;
			char* ifname = "can%<module_id_par>";

			can_%<module_id_par> = socket(PF_CAN, SOCK_RAW, CAN_RAW);
			if (can_%<module_id_par> == -1) {
				printf("CAN socket create error.\n");
			}
			if(fcntl(can_%<module_id_par>, F_SETFL, O_NONBLOCK) < 0) {
				printf("CAN socket set O_NONBLOCK error.\n");
			}
			memset(&addr, 0, sizeof(addr));
			addr.can_family = AF_CAN;
			strncpy(ifr.ifr_name, ifname, sizeof(ifr.ifr_name) -1);
			if (ioctl(can_%<module_id_par>, SIOCGIFINDEX, &ifr) < 0) {
				printf("CAN socket ioctl SIOCGIFINDEX error.\n");
			}
			addr.can_ifindex = ifr.ifr_ifindex;
			
			if (bind(can_%<module_id_par>, (struct sockaddr *)(void*)&addr, sizeof(addr)) < 0) {
				printf("CAN socket bind error.\n");
			}
			printf("opening %s\n", ifname);
		}
	%endif
%endfunction

%% Function: Outputs ===========================================================
%function Outputs(block, system) Output

	%if !SLibCodeGenForSim()
	%assign module_id_par = LibBlockParameterValue(module_id, 0)
	%assign message = LibBlockOutputSignal(1, "", "", 1)

	{
		struct can_frame sc_frame;
		int ret;
		int dlc;

		while ((ret = recv(can_%<module_id_par>, &sc_frame, sizeof(sc_frame), 0)) > 0) {
			if (ret < sizeof(sc_frame)) {
				continue;
			} else {
				dlc = sc_frame.can_dlc;
				if (dlc > 8)
					dlc = 8;

				%% CAN_MESSAGE
				memset(&%<message>, 0, sizeof(%<message>));
				%<message>.Length = dlc;
				%<message>.ID = sc_frame.can_id & CAN_EFF_MASK;
				%<message>.Extended = sc_frame.can_id & CAN_EFF_FLAG? 1: 0;
				int i;
				for (i = 0; i < dlc; i++ ) {
					%<message>.Data[i] = sc_frame.data[i];
				}
				

				%% Call a function to process the received message via function-call subsystem
				%foreach callIdx = NumSFcnSysOutputCalls
					%if LibIsEqual(SFcnSystemOutputCall[callIdx].BlockToCall,"unconnected")
						%continue
					%endif
					%% call the downstream system
					%<LibBlockExecuteFcnCall(block, callIdx)>\
				%endforeach
			}
		}
	}
	%endif
%endfunction

%% [EOF]
