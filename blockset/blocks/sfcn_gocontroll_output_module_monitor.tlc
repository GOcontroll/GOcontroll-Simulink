%%***************************************************************************************
%% file         sfcn_gocontroll_output_module_monitor.tlc
%% brief        Target Language Compiler file that contains the code generation specifics
%%              for an S-function with the same name.
%%
%%---------------------------------------------------------------------------------------
%%                          C O P Y R I G H T
%%---------------------------------------------------------------------------------------
%%  Copyright 2024 (c) by GOcontroll      http://www.gocontroll.com     All rights reserved
%%
%%---------------------------------------------------------------------------------------
%%                            L I C E N S E
%%---------------------------------------------------------------------------------------
%% Permission is hereby granted, free of charge, to any person obtaining a copy of this
%% software and associated documentation files (the "Software"), to deal in the Software
%% without restriction, including without limitation the rights to use, copy, modify, merge,
%% publish, distribute, sublicense, and/or sell copies of the Software, and to permit
%% persons to whom the Software is furnished to do so, subject to the following conditions:
%%
%% The above copyright notice and this permission notice shall be included in all copies or
%% substantial portions of the Software.
%%
%% THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
%% INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR
%% PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE
%% FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR
%% OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
%% DEALINGS IN THE SOFTWARE.
%%
%%***************************************************************************************
%implements  sfcn_gocontroll_output_module_monitor "C"

%% add required include files
%<LibAddToCommonIncludes("GocontrollOutputModule.h")>

%% Function: Start ==========================================================
%%
%% Purpose:
%%      Code generation for initialization
%%
%function Start(block, system) Output
	%assign slot = "::slot%<block.RTWdata.moduleSlot>"
	%assign slotmon = "::slot%<block.RTWdata.moduleSlot>mon"
	%if EXISTS(%<slot>) == 0
		%<LibBlockReportError(block, "No corresponding 6 channel output module block found in the same slot")>
	%else
		%assign tmp = %<slot>
		%assign type = "6 channel output module"
		%if ISEQUAL(tmp, type)
			%if EXISTS(%<slotmon>) == 0
				%assign %<slotmon> = type
			%else
				%assign warning = "Module slot %<block.RTWdata.moduleSlot> is already being monitored, if that is your intention you can ignore this warning"
				%<LibBlockReportWarning(block, warning)>
			%endif
		%else
			%assign error = "The corresponding slot is configured as a %<tmp>, not a 6 channel output module"
			%<LibBlockReportError(block, error)>
		%endif
	%endif
%endfunction

%% Function: Output ==========================================================
%%
%% Purpose:
%%      Code generation for signal output
%%
%function Outputs(block, system) Output
	%%

	%<LibBlockOutputSignal(0, "", "", 0)> = outputModule%<block.RTWdata.moduleSlot>.temperature;
	%<LibBlockOutputSignal(1, "", "", 0)> = outputModule%<block.RTWdata.moduleSlot>.ground;
	%<LibBlockOutputSignal(2, "", "", 0)> = outputModule%<block.RTWdata.moduleSlot>.supply;
	%<LibBlockOutputSignal(3, "", "", 0)> = outputModule%<block.RTWdata.moduleSlot>.errorCode;
	%assign outputIdx = 4
	%foreach channel = 6
		%<LibBlockOutputSignal(outputIdx, "", "", 0)> = outputModule%<block.RTWdata.moduleSlot>.current[%<channel>];
		%assign outputIdx = outputIdx + 1
		%assign duty = "block.RTWdata.C%<channel + 1>duty"
		%assign y = "1"
		%if ISEQUAL(%<duty>, y)
			%<LibBlockOutputSignal(outputIdx, "", "", 0)> = outputModule%<block.RTWdata.moduleSlot>.dutyCycle[%<channel>];
			%assign outputIdx = outputIdx + 1
		%endif
	%endforeach
  %%
%endfunction

%%******************************* end of sfcn_gocontroll_output_module_monitor.tlc ***************************